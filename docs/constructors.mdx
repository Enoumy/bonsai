---
title: "1: Constructors"
uuid: 05d4875b-921d-4ce5-aaf6-44c897f1720b
parent: "./api_tour.mdx"
---

<div id="floater">

```
type ('input, 'model, 'result) t
```

| Parameter            | Description             |
| -------------------- | ----------------------- |
| <tt>**'input**</tt>  | Input to component      |
| <tt>**'model**</tt>  | Component state-machine |
| <tt>**'result**</tt> | Type of computed value  |

</div>

<!--
```ocaml
# open! Bonsai_mdx
# open! Core_kernel
```
-->

## <tt>Bonsai.of\_module</tt>

`Bonsai.of_module` is the most powerful and common component constructor in the
API.  When a module implements the `Bonsai.S` interface, `Bonsai.of_module` can
be called on the first-class representation of that module.

```ocaml
module type S = sig
  (** The name of the component.  This is used to identify the component while
  debugging, and to annotate error messages.  If you can't think of a good
  name, a reasonable fallback is [Source_code_position.to_string [%here]]. *)
  val name : string

  (** A component receives read-only input, either as output from other
  components or from an external system (e.g. data from a server).  The input
  is frequently dynamic, but may also be constant. *)
  module Input : sig
    type t
  end

  (** A component's model is a state-machine that the component can read, but
  also write to.  Because both the input and model are readable, it can be hard
  to decide whether to request some data from the input or the model.  It is
  highly recommended to put just the data that needs mutation in [Model.t], and
  the rest in [Input.t]. *)
  module Model : sig
    type t
  end

  (** Components can change their own [Model.t] by issuing "actions" that
  perform the state transition.  If you think of the state machine as
  having state-nodes of type [Model.t], then the arrows between those nodes
  would be of type [Action.t]. *)
  module Action : sig
    type t [@@deriving sexp_of]
  end

  (** While UI components stereotypically produce some kind of "view", with
  Bonsai, components are small and easy enough to compose that Bonsai
  components frequently produce intermediate results which are then wired into
  other components. *)
  module Result : sig
    type t
  end

  (** [compute] is a function from input and model to the component's result.
  In a component that produces a view, this function could be thought of as the
  "view computation function".

  This function is also given an "inject" function which converts this
  component's [Action.t] to a global [Event.t] which can be given to
  Bonsai to schedule.  Frequently, this Event.t is embedded within the
  result as a handler for some kind of user input. *)
  val compute : inject:(Action.t -> Event.t) -> Input.t -> Model.t -> Result.t

  (** When an action is raised by this component (via an Event.t), Bonsai
  will eventually pass that action back to that component's [apply_action]
  function.  This function is responsible for looking at the model and the
  incoming action and producing a new model.

  If [Model.t] represents a state-node in a state-machine, then apply_action is
  the implementation of transitions between states. *)
  val apply_action
    :  inject:(Action.t -> Event.t)
    -> schedule_event:(Event.t -> unit)
    -> Input.t
    -> Model.t
    -> Action.t
    -> Model.t
end
```

For more on the state-machine like nature of a bonsai component, read the
[Bonsai Concepts](./concepts.md) page.

```ocaml
# #show Bonsai.of_module
val of_module :
  ('input, 'model, 'action, 'result) Bonsai.component_s ->
  ('input, 'model, 'result) Bonsai.t
```

When implementing `Bonsai.S` and `Bonsai.of_module`, it is common to
have a
component take up an entire file, with the submodules and functions declared as
modules and functions at the top-most level of that file.


### Examples

The most useless module that implements `Bonsai.S`:

```ocaml
module Useless : sig
  (* This is typically a whole mli file *)

  include Bonsai.S
    with type Input.t  = unit
     and type Model.t  = unit
     and type Result.t = unit
end = struct
  (* This is typically a whole ml file *)

  module Input = Unit
  module Model = Unit
  module Action = Unit
  module Result = Unit

  let name = "useless"
  let compute ~inject:_ () () = ()
  let apply_action ~inject:_ ~schedule_event:_ () () () = ()
end
```

And then using that module to make a useless `Bonsai.t` component.

```ocaml
# let useless = Bonsai.of_module (module Useless)
val useless : (unit, unit, unit) Bonsai.t = <abstr>
```

A basic component that tracks an integer in the state machine, and whose result
is both the view of that integer, and also some buttons that trigger actions
to increment or decrement the counter.

<!--
```ocaml env=e1
# open! Bonsai_mdx
# open! Core_kernel
```
-->

```ocaml env=e1
module Counter : sig
  (* This is typically a whole mli file *)

  include Bonsai.S
    with type Input.t = unit
     and type Model.t = int
     and type Result.t = Vdom.Node.t
end = struct
  (* This is typically a whole ml file *)

  module Input = Unit
  module Model = Int
  module Result = Vdom.Node

  let name = "Counter"

  module Action = struct
    type t =
      | Increment
      | Decrement
    [@@deriving sexp_of]
  end

  let apply_action ~inject:_ ~schedule_event:_ () model = function
    | Action.Increment -> model + 1
    | Action.Decrement -> model - 1

  let compute ~inject () model =
    let button label action =
      let on_click = Vdom.Attr.on_click (fun _ -> inject action) in
      Vdom.Node.button [ on_click ] [ Vdom.Node.text label ]
    in
    Vdom.Node.div
      []
      [ button "-1" Action.Decrement
      ; Vdom.Node.text (Int.to_string model)
      ; button "+1" Action.Increment
      ]
end
```

And then using that module to make a component.

```ocaml env=e1
# let counter = Bonsai.of_module (module Counter);;
val counter : (unit, int, Counter.Result.t) Bonsai.t = <abstr>
```

<!-- Use the <tt> element to get monospace text without the bright-blue that
comes with `code blocks` -->
## <tt>Bonsai.const</tt>

`Bonsai.const` creates a Bonsai component with an unchanging output.  It does
not have an input or model. [^tick-a-model-and-input]  Compare to `Fn.const`.

```ocaml
# Bonsai.const
- : 'result -> ('a, 'b, 'result) Bonsai.t = <fun>
```

A constant component is not used frequently, but can be useful when an API that
you want to call requires a component.

### Examples

```ocaml
# let the_answer = Bonsai.const 42
val the_answer : ('_weak1, '_weak2, int) Bonsai.t = <abstr>
```

## <tt>Bonsai.pure</tt>

`Bonsai.pure` is used to create a Bonsai component that can be implemented as a
pure function from `'input` to `'result`.  A component created in this way can
not raise actions, and has no `'model` state-machine. [^tick-a-model]

```ocaml
# #show Bonsai.pure
val pure : f:('input -> 'result) -> ('input, 'a, 'result) Bonsai.t
```

### Examples

A component whose input is an OCaml string, and whose result is a Virtual DOM text
node that will display the contents of that string.

```ocaml
# let string_to_vdom_text = Bonsai.pure ~f:(fun string -> Vdom.Node.text string);;
val string_to_vdom_text : (string, '_weak3, Vdom.Node.t) Bonsai.t = <abstr>
```

A component whose input is a set of usernames, and whose result is a text
representation summarizing the set of people that like your twitter "content".

```ocaml
# let users_to_summary =
  Bonsai.pure ~f:(fun user_set ->
    let render_cutoff = 5 in
    match Set.length user_set with
    | 0 -> Vdom.Node.text "no one liked your post :("
    | n ->
      let first_5_people =
        user_set |> Set.to_list |> Fn.flip List.take render_cutoff |> String.concat ~sep:", "
      in
      if n <= render_cutoff
      then Vdom.Node.text (sprintf "%s liked your post" first_5_people)
      else (
        let others_count = n - render_cutoff in
        Vdom.Node.text
          (sprintf "%s and %d others liked your post" first_5_people others_count)))
val users_to_summary :
  ((string, '_weak4) Set.t, '_weak5, Vdom.Node.t) Bonsai.t = <abstr>
```

## <tt>Bonsai.pure\_from\_model</tt>

Very similar to `Bonsai.pure`, `Bonsai.pure_from_model` is used to create
a Bonsai component implementable as a pure function.  Where it differs from `pure`
is that `pure_from_model` reads from the `'model` of the component and has no
`'input` [^tick-a-input]

```ocaml
# #show Bonsai.pure_from_model
val pure_from_model : f:('model -> 'result) -> ('a, 'model, 'result) Bonsai.t
```

It's not obvious why you would use this function instead of `of_pure`, but
occasionally it's easier to combine a component created via this function
with a component that requires the model parameter than it is to combine
components that don't share the same model.

Put another way, it's easier to combine these components:

```
val a : (unit, int, _) Bonsai.t
val b : (unit, int, _) Bonsai.t
```

than it would be to combine these:

```
val c : (unit, int,  _) Bonsai.t
val d : (int,  unit, _) Bonsai.t
```

### Examples

A component whose model is a string, and whose result is a Virtual DOM text
node that will display the contents of that string.  Even though the string is
in the `'model` position, it is not actually editable from within the
component.

Let's assume that there is a textbox component that has a `string` model to store
the text currently in the textbox.

<!--
```ocaml
let textbox : (unit, string, Vdom.Node.t) Bonsai.t = Bonsai.pure ~f:(fun () -> failwith "")
```
-->

```ocaml
# #show textbox
val textbox : (unit, string, Vdom.Node.t) Bonsai.t
```

We'd like to display that textbox alongside a display of how many characters
you've typed into the textbox.  The way that both of these components access
the string (writing to it from the textbox, and reading from it from the
display) will be by sharing the same model.

Because the display will only be reading the string, we start by using `pure`,
but a first attempt using `pure` fails with a type error.

```ocaml
# let string_length_display s =
      s
      |> String.length
      |> Int.to_string
      |> Vdom.Node.text
val string_length_display : string -> Vdom.Node.t = <fun>

# let combined =
    let%map.Bonsai textbox = textbox
    and            count   = Bonsai.pure ~f:string_length_display in
    Vdom.Node.div [] [textbox; count]
Line 3, characters 30-66:
Error: This expression has type (string, 'a, Vdom.Node.t) Bonsai.t
       but an expression was expected of type (unit, string, 'b) Bonsai.t
       Type string is not compatible with type unit 
```

But if we change the call from `pure` to `pure_from_model` the models line up
correctly.

```ocaml
# let combined =
    let%map.Bonsai textbox = textbox
    and            count   = Bonsai.pure_from_model ~f:string_length_display in
    Vdom.Node.div [] [textbox; count]
val combined : (unit, string, Vdom.Node.t) Bonsai.t = <abstr>
```


[^tick-a-model-and-input]: Because the component is parametric over both input
  and model (`('a, 'b, result) Bonsai.t`), it can be used in any place that
  requires a component with any input or model.  The type signature means that
  the component does not read or write to the model, or does not read from the
  input.

[^tick-a-model]: Similarly to the above, when the `'model` parameter in a
  Bonsai component is parametric, it signifies that the component does not read
  or write to the model, and because of that, it can use any model.

[^tick-a-input]: When the `'input` parameter in a Bonsai component is
  parametric, it signifies that the component does not read from the input, so
  the input can be anything.
